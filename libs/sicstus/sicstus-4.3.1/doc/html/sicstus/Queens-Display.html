<html lang="en">
<head>
<title>Queens Display - SICStus Prolog</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="SICStus Prolog">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Tk.html#Tk" title="Tk">
<link rel="prev" href="What-We-Have-Left-Out-_0028Tk_0029.html#What-We-Have-Left-Out-_0028Tk_0029" title="What We Have Left Out (Tk)">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
<link href="texinfo.css" rel="stylesheet" type="text/css">
</head>
<body>
<div class="node">
<a name="Queens-Display"></a>
<p>
Previous:&nbsp;<a rel="previous" accesskey="p" href="What-We-Have-Left-Out-_0028Tk_0029.html#What-We-Have-Left-Out-_0028Tk_0029">What We Have Left Out (Tk)</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Tk.html#Tk">Tk</a>
<hr>
</div>

<h5 class="subsubsection">10.37.3.9 Example pure Tcl/Tk program</h5>

<p>To show some of what can be done with Tcl/Tk, we will show an example of
part of a GUI for an 8-queens program.  Most people will be
familiar with the 8-queens problem: how to place 8 queens on a chess
board such that they don't attack each other according to the normal
rules of chess.

   <p>Our example will not be a program to solve the 8-queens problem
(that will come later in the tutorial) but just the Tcl/Tk part for
displaying a solution.  The code can be found in
<code>library('tcltk/examples/ex18.tcl')</code>.

   <p>The way an 8-queens solution is normally presented is as a list of
numbers. The position of a number in the list indicates the column the
queens is placed at and the number itself indicates the row. For
example, the Prolog list <code>[8, 7, 6, 5, 4, 3, 2, 1]</code> would indicate
8 queens along the diagonal starting a column 1, row 8 and finishing at
column 8 row 1.

   <p>The problem then becomes, given this list of numbers as a solution,
how to display the solution using Tcl/Tk. This can be divided into
two parts: how to display the initial empty chess board, and how
to display a queen in one of the squares.

   <p>Here is our code for setting up the chess board:

<pre class="example">
                                  <div align="right"><em>% ex18.pl</em>
     </div>
     #! /usr/bin/wish
     
     proc setup_board { } {
         # create container for the board
         frame .queens
     
         # loop of rows and columns
         for {set row 1} {$row &lt;= 8} {incr row} {
             for {set column 1} {$column &lt;= 8} {incr column} {
     
                 # create label with a queen displayed in it
                 label .queens.$column-$row -bitmap @bitmaps/q64s.bm -relief flat
     
                 # choose a background color depending on the position of the
                 # square; make the queen invisible by setting the foreground
                 # to the same color as the background
                 if { [expr ($column + $row) % 2] } {
                     .queens.$column-$row config -background #ffff99
                     .queens.$column-$row config -foreground #ffff99
                 } else {
                     .queens.$column-$row config -background #66ff99
                     .queens.$column-$row config -foreground #66ff99
                 }
     
                 # place the square in a chess board grid
                 grid .queens.$column-$row -row $row -column $column -padx 1 -pady 1
             }
         }
         pack .queens
     }
     
     setup_board
</pre>
   <p>The first thing that happens is that a frame widget is created to
contain the board.  Then there are two nested loops that loop over the
rows and columns of the chess board.  Inside the loop, the first thing
that happens is that a label widget is created. It is named using the
row and column variables so that it can be easily referenced later.  The
label will not be used to display text but to display an image, a bitmap
of a queen. The label creation command therefore has the special
argument <samp><span class="option">-bitmap @q64s.bm</span></samp>, which says that the label
will display the bitmap loaded from the file <samp><span class="file">q64s.bm</span></samp>.

<!-- (This queen bitmap we use was borrowed from one of those used in the -->
<!-- GnuChess program.) -->
   <p>The label with the queen displayed in it has now been created. 
The next thing that happens is that the background color of
the label (square) is chosen. Depending on the position of
the square it becomes either a &ldquo;black&rdquo; or a &ldquo;white&rdquo; square. 
At the same time, the foreground color is set to the background color. 
This is so that the queen (displayed in the foreground color) will
be invisible, at least when the board is first displayed.

   <p>The final action in the loop is to place the label (square) in relation
to all the other squares for display. A chess board is a simple grid of
squares, and so this is most easily done through the <code>grid</code> geometry
manager.

   <p>After the board has been set up square-by-square it still needs to be displayed,
which is done by <code>pack</code>-ing the outermost frame widget.

   <p>To create and display a chess board widget, all that is needed
is to call the procedure

<pre class="example">     setup_board
</pre>
   <p class="noindent">which creates the chess board widget.

   <p>Once the chess board has been displayed, we need to be able to take a
solution, a list of rows ordered by column, and place queens in the
positions indicated.

   <p>Taking a topdown approach, our procedure for taking a solution
and displaying is as follows:

<pre class="example">     proc show_solution { solution } {
         clear_board
         set column 1
         foreach row $solution {
             place_queen $column $row
             incr column
         }
     }
</pre>
   <p>This takes a solution in <code>solution</code>, clears the board of all queens, and
then places each queen from the solution on the board.

   <p>Next we will handle clearing the board:

<pre class="example">     proc clear_board { } {
         for { set column 1 } {$column &lt;= 8} {incr column} {
             reset_column $column
         }
     }
     
     proc reset_column { column } {
         for {set row 1 } { $row &lt;= 8 } {incr row} {
             set_queens $column $row off
         }
     }
     
     proc set_queens { column row state } {
         if { $state == "on" } {
             .queens.$column-$row config -foreground black
         } else {
             .queens.$column-$row config
             -foreground [.queens.$column-$row cget -background]
         }
     }
</pre>
   <p>The procedure <code>clear_board</code> clears the board of queens by calling
the procedure <code>reset_column</code> for each of the 8 columns on a board. 
<code>reset_column</code> goes through each square of a column and sets the
square to <code>off</code> through <code>set_queens</code>.  In turn, <code>set_queens</code>
sets the foreground color of a square to black if the square is turned
<code>on</code>, thus revealing the queen bitmap, or sets the foreground color of a
square to its background color, thus making the queens invisible, if it
is called with something other than <code>on</code>.

   <p>That handles clearing the board, clearing a column or turning a
queen on or off on a particular square.

   <p>The final part is <code>place_queen</code>:

<pre class="example">     proc place_queen { column row } {
         reset_column $column
         set_queens $column $row on
     }
</pre>
   <p>This resets a column so that all queens on it are invisible and then
sets the square with coordinates given in <code>row</code> and <code>column</code>
to on.

   <p>A typical call would be:

<pre class="example">     show_solution "1 2 3 4 5 6 7 6 8"
</pre>
   <!-- INCLUDE @@caption figure 18: 8-queens display in Tcl/Tk -->
<!-- INCLUDE @@ex18.tcl -->
   <pre class="sp">

</pre>
<img src="images/tcltkex18.png" alt="images/tcltkex18.png">
<div align="center">8-Queens Display In Tcl/Tk</div>

<p class="noindent">which would display queens along a diagonal. 
(This is of course not a solution to the 8-queens problem. This Tcl/Tk
code only displays possible queens solutions; it doesn't check if the
solution is valid.  Later we will combine this Tcl/Tk display code with
Prolog code for generating solutions to the 8-queens problem.)

<div class="logo">
<a href="http://sicstus.sics.se/">
<table><tr><td>&nbsp;</td></tr></table>
</a>
</div>
<div class="node">
<ul class="node">
<li><a href="index.html#Top">User's Manual</a>
<hr>
<li><a href="index.html#TOC">Table of Contents</a>
</ul>
</div>
<hr>
<a HREF="mailto:sicstus-support@sics.se?subject=Documentation%20feedback%20on%20html/sicstus/Queens-Display.html&amp;body=Feedback%20on%20documentation%20node%20html/sicstus/Queens-Display.html%20in%20User's%20Manual.">Send feedback on this subject.</a>
   </body></html>

